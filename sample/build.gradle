/*
 * Copyright 2014 Alex Curran
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        google()
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.1.1'
    }
}

repositories {
    google()
    jcenter()
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots'
    }
}

apply plugin: 'com.android.application'

dependencies {
    // implementation project(':library')
    implementation 'com.github.amlcurran.showcaseview:library:5.4.3'
    implementation 'com.android.support:support-v4:23.1.1'
    implementation 'com.nineoldandroids:library:2.4.0'
    implementation 'com.android.support:appcompat-v7:23.1.1'
}

android {
    compileSdkVersion Integer.parseInt(COMPILE_SDK)

    defaultConfig {
        versionName parseVersionName()
        versionCode Integer.parseInt(VERSION_CODE)
        targetSdkVersion Integer.parseInt(TARGET_SDK)
        minSdkVersion Integer.parseInt(MIN_SDK_SAMPLE)
    }

    signingConfigs {
        release {
            storeFile file(project.hasProperty("SHOWCASEVIEW_KEYSTORE_LOCATION") ? SHOWCASEVIEW_KEYSTORE_LOCATION : "/Users/amlcurran/.android/debug.keystore")
            storePassword returnDefault("SHOWCASEVIEW_KEYSTORE_PW")
            keyAlias returnDefault("SHOWCASEVIEW_KEY_ALIAS")
            keyPassword returnDefault("SHOWCASEVIEW_KEY_PW")
        }
    }


    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }
    lint {
        abortOnError false
    }
}

def returnDefault(String propertyName) {
   if (project.hasProperty(propertyName)) {
       return project.property(propertyName)
   } else {
       return ""
   }
}

def parseVersionName() {
    if (project.hasProperty("versionName")) {
        return project.property("versionName")
    } else {
        println("No version name supplied, using default")
        return "0"
    }
}
